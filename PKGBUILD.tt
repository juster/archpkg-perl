[%# -*- mode: shell-script -*- -%]
# $Id: PKGBUILD 54319 2009-10-08 13:26:47Z francois $
# Maintainer: kevin <kevin.archlinux.org>
# Contributor: judd <jvinet.zeroflux.org>
# Contributor: francois <francois.archlinux.org> 
# Contributor: Justin Davis <juster.cpan.org>
pkgname=perl
pkgver=5.12.0
pkgrel=0
pkgdesc="Practical Extraction and Report Language"
arch=(i686 x86_64)
license=('GPL' 'PerlArtistic')
url="http://www.perl.org"
groups=('base')
depends=('gdbm' 'db>=4.8' 'coreutils' 'glibc' 'sh')
source=(http://www.perl.com/CPAN/src/perl-${pkgver}.tar.bz2
    perlbin.sh )
provides=(
[% provides %]
)
options=('!makeflags' '!purge')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  if [ "${CARCH}" = "x86_64" ]; then
    # for x86_64
    arch_opts="-Dcccdlflags='-fPIC'"
  else 
    # for i686
    arch_opts=""
  fi
  ./Configure -des \
    -Dusethreads -Doptimize="${CFLAGS}" -Dprefix=/usr \
    -Dinstallprefix=${pkgdir}/usr -Dvendorprefix=/usr \
    -Dprivlib=/usr/share/perl5/core_perl \
    -Darchlib=/usr/lib/perl5/core_perl \
    -Dsitelib=/usr/share/perl5/site_perl/${pkgver} \
    -Dsitearch=/usr/lib/perl5/site_perl/${pkgver} \
    -Dvendorlib=/usr/share/perl5/vendor_perl \
    -Dvendorarch=/usr/lib/perl5/vendor_perl \
    -Dotherlibdirs=/usr/lib/perl5/current:/usr/lib/perl5/site_perl/current \
    -Dscriptdir='/usr/bin/perlbin/core' \
    -Dsitescript='/usr/bin/perlbin/site' \
    -Dvendorscript='/usr/bin/perlbin/vendor' \
    -Dinc_version_list=none \
    -Dman1ext=1perl -Dman3ext=3perl ${arch_opts}

  ( make && make test ) || return 1
}

package() {
  make install

  ( cd "${pkgdir}/usr/bin"
    mv "perl${pkgver}" perl )

  ### Perl Settings ###
  # Change man page extensions for site and vendor module builds.
  sed -e '/^man1ext=/ s/1perl/1p/' -e '/^man3ext=/ s/3perl/3pm/' \
      -i ${pkgdir}/usr/lib/perl5/core_perl/Config_heavy.pl

  ### CPAN Settings ###
  # Set CPAN default config to use the site directories.
  sed -e '/(makepl_arg =>/   s/""/"INSTALLDIRS=site"/' \
      -e '/(mbuildpl_arg =>/ s/""/"installdirs=site"/' \
      -i ${pkgdir}/usr/share/perl5/core_perl/CPAN/FirstTime.pm

  ### CPANPLUS Settings ###
  # Set CPANPLUS default config to use the site directories.
  sed -e "/{'makemakerflags'}/ s/'';/'INSTALLDIRS=site';/" \
      -e "/{'buildflags'}/     s/'';/'installdirs=site';/" \
      -i ${pkgdir}/usr/share/perl5/core_perl/CPANPLUS/Config.pm

  # Profile script so set paths to perl scripts.
  install -D -m755 ${srcdir}/perlbin.sh \
                   ${pkgdir}/etc/profile.d/perlbin.sh

  # Convert hard links to symbolic links
  ( cd "${pkgdir}/usr/bin/perlbin/core"
    ln -sf c2ph    pstruct
    ln -sf s2p     psed
    ln -sf perlbug perlthanks
  )

  # This just replaces Config_heavy.pl, let's explicitly do this...
  # grep -Rl "${pkgdir}" "${pkgdir}/usr" | \
  #     xargs sed -i "s^${pkgdir}^^g"

  # Remove all pod files *except* those under /usr/share/perl5/core_perl/pod/
  # (FS#16488)
  {
      find "${pkgdir}/usr/share/perl5/core_perl" \
          \( -name 'pod' -prune -o -name '*.pod' \) -type f -print0
      find "${pkgdir}/usr/lib" -name '*.pod'
  } | xargs -0r rm -f
}

md5sums=('3e15696f4160775a90f6b2fb3ccc98c2'
         '9c4e3e56d71f123e92a68986df5ea924')
